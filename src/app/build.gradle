apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'

group='com.github.uphold'

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'
    publishNonDefault true
    testBuildType "debug"

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 19
        versionName "0.15.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    defaultPublishConfig "productionRelease"

    lintOptions {
        abortOnError false
    }

    packagingOptions {
        exclude 'LICENSE.txt'
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    flavorDimensions "default"

    productFlavors {
        production {
            dimension 'default'
            buildConfigField 'String', 'API_SERVER_URL', '"https://api.uphold.com"'
            buildConfigField 'String', 'AUTHORIZATION_SERVER_URL', '"https://uphold.com"'
        }

        sandbox {
            dimension 'default'
            buildConfigField 'String', 'API_SERVER_URL', '"https://api-sandbox.uphold.com"'
            buildConfigField 'String', 'AUTHORIZATION_SERVER_URL', '"https://sandbox.uphold.com"'
        }
    }
}

if(android.productFlavors.size() > 0) {
    android.libraryVariants.all { variant ->
        if(variant.name == android.defaultPublishConfig) {
            def bundleTask = tasks["bundle${name.capitalize()}"]
            artifacts {
                archives(bundleTask.archivePath) {
                    classifier null
                    builtBy bundleTask
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.darylteo:rxjava-promises-java:1.2.1'
    implementation 'com.jakewharton.retrofit:retrofit1-okhttp3-client:1.1.0'
    implementation 'com.squareup.okhttp3:okhttp:3.9.1'

    androidTestImplementation 'com.android.support.test:testing-support-lib:0.1'
    androidTestImplementation 'org.hamcrest:hamcrest-library:1.3'
    androidTestImplementation ('com.github.javafaker:javafaker:0.5')
    {
        exclude group: 'org.yaml' // The version supported by android is at the libs folder.
    }
    androidTestImplementation files ('libs/snakeyaml-1.10-android.jar')

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.apache.commons:commons-lang3:3.4'
    testImplementation 'org.powermock:powermock-api-mockito:1.6.4'
    testImplementation 'org.powermock:powermock-module-junit4:1.6.4'
}
